apply plugin: "java"
apply plugin: "com.google.protobuf"
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.12'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation deps.grpc_protobuf
    implementation deps.grpc_stub
    implementation deps.grpc_validate
}

def generatedSrcRoot = file("${buildDir}/generated/source/proto/main/java/")
sourceSets.main {
    proto {
        srcDir "${rootDir}/base-storage/proto/"
    }
    java {
        srcDirs = ["${generatedSrcRoot}"]
    }
}

compileJava {
    options.annotationProcessorGeneratedSourcesDirectory generatedSrcRoot
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${versions.grpcProtobuf}"
        }
        javapgv {
            artifact = "io.envoyproxy.protoc-gen-validate:protoc-gen-validate:0.1.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            javapgv {
                option "lang=java"
            }
        }
    }
}

jar {
    baseName = 'base-storage'
    manifest {
        attributes('kin-storage': project.name,
                'Version': project.version)
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? bintrayKey : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']
    pkg {
        repo = "kin-android"
        userOrg = "kinecosystem"
        name = "base-storage"
        desc = "Kin Android SDK Base Storage"
        websiteUrl = "https://github.com/kinecosystem/kin-android"
        vcsUrl = "https://github.com/kinecosystem/kin-android.git"
        licenses = ["MIT", "Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
    }
}
